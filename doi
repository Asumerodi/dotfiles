#!/usr/bin/zsh
setopt extendedglob

# global constants
SCRIPT_NAME="${0:t}"
SCRIPTDIR="${0:A:h}"
TMPDIR=$( mktemp -d /tmp/doi.XXXXXXXXXXXXXXXX )
SRCDIR="${SCRIPTDIR}/src"

readonly \
  SCRIPT_NAME \
  SCRIPTDIR \
  TMPDIR \
  SRCDIR

##### CONVEINEICE FUNCTIONS #####

# alias to simplify checking if a command is in $path
alias exists="> /dev/null type -p"

# standard error message
dot_err () {
  print -u2 "unrecoverable error:" "$@"
}

# remove TMPDIR on exit ensuring that it matches spec first
finish () {
  if [[ ${TMPDIR} =~ '^/tmp/doi\.([0-9a-zA-Z]){16}$' ]]; then
    rm -rf --one-file-system --preserve-root ${TMPDIR}
  fi
}

finish::failed () {
  finish
  exit 1
}

finish::success () {
  finish

  if [[ -n ${backupmessage} ]]; then
    for message in ${backupmessage}; do
      print "${message} successfully backed up to ${BACKUPDIR}"
    done
  fi

  exit 0
}

##########

# chech zsh version matches requirement
if ! [[ $ZSH_VERSION > 5.2 || $ZSH_VERSION == 5.2 ]]; then
  printf "zsh version must be 5.2 or higher\n"
  finish::failed
fi

# fail on pipe
if [[ ${SCRIPT_NAME} == "zsh" ]]; then
  dot_err "you cannot pipe this script"
  finish::failed
fi

# sources
. ${SCRIPTDIR}/zsh-functional/functional.plugin.zsh

sources () {
  local sourable=$1
  if ! . ${SRCDIR}/${sourable} &> /dev/null; then
    dot_err "failed to find include: ${sourable}"
    finish::failed
  fi
}

# call sources on sourables
eachf sources options dependencies installd

usage () {
  cat <<EOF
Usage: ${SCRIPT_NAME} <command> <target> [options]

  COMMANDS:
    ln   Create symlinks for selected dotfiles (default: zsh only)
    rm   Remove symlinks and any files created during ln

  TARGETS:
    -a   Install all     dotfiles
    -z   Install Zsh     dotfiles
    -t   Install Tmux    dotfiles
    -g   Install GHCi    dotfiles
    -n   Install Neovim  dotfiles
    -x   Install X11     dotfiles

  OPTIONS:
    -i   Install package dependencies for chosen dotfiles (ln only)
    -f   Force ln to backup existing files instead of failing
    -v   Verbose output
EOF
}

main () {
  # various paths used throughout the script
  local -r \
    ZSHDIR="${SCRIPTDIR}/zsh" \
    GHCIDIR="${SCRIPTDIR}/ghci" \
    NVIMDIR="${SCRIPTDIR}/neovim" \
    X11DIR="${SCRIPTDIR}/xmonad" \
    TMUXDIR="${SCRIPTDIR}/tmux" \
    DISTDIR="${SCRIPTDIR}/dist" \
    BACKUPDIR="${SCRIPTDIR}/backup" \
    BINDIR="${HOME}/.local/bin"

  # the command being called
  local CMD

  # for dependencies checker and backup success printer
  local -Ua \
    missing_dependencies \
    backupmessage

  # for manual flag parsing
  local -a flags

  # distro dependencies for arch and ubuntu
  local -Ar YAOURT_DEPENDENCIES=( \
    zsh     "antibody fzf hub tig lsof trash-cli git" \
    ghci    "ghc haskell-stack haskell-hscolour haskell-pretty-show" \
    tmux    "tmux bind-tools acpi git powerline-fonts-git" \
    neovim  "neovim python-neovim python \
              ghc-mod rust-racer clang shellcheck \
              eslint tidy go git" \
    x11     "xmonad qutebrowser mpv xss-lock-git xautolock \
              xbanish unclutter-xfixes-git slock xorg-xsetroot \
              compton xmonad-contrib maim farbfeld xorg-xset git"
  )

  local -Ar APT_DEPENDENCIES=( \
    zsh     "antibody lsof trash-cli hub tig git" \
    ghci    "ghc haskell-stack libghc-hscolour-dev libghc-pretty-show-dev" \
    tmux    "tmux acpi dnsutils git" \
    neovim  "neovim python3 python3-pip ghc-mod clang shellcheck \
              tidy golang git" \
    x11     "xmonad mpv xss-lock xautolock suckless-tools \
              compton libghc-xmonad-contrib-dev maim \
              x11-xserver-utils qutebrowser git"
  )

  # respect users current $XDG_CONFIG_HOME if set
  if [[ ${XDG_CONFIG_HOME} != ${HOME}/.config ]]; then
    local -r CONFIGDIR="${XDG_CONFIG_HOME}"
  else
    local -r CONFIGDIR="${HOME}/.config"
  fi

  # exit trap for consistently clean exit
  trap "trap - SIGTERM && finish && kill -- -$$" \
    SIGINT SIGTERM EXIT

  options $@

  # mark readonly to prevent modification from this point on
  readonly flags CMD

  # whether to be verbose or not
  if [[ ${verbose} == 1 ]]; then
    local quiet=/dev/tty
  else
    local quiet=/dev/null
  fi

  dependencies

  installd

  finish::success
}

 main $@
