#!/usr/bin/zsh
setopt extendedglob

# global constants
SCRIPT_NAME="${0:t}"
SCRIPTDIR="${0:A:h}"
TMPDIR=$( mktemp -d /tmp/doi.XXXXXXXXXXXXXXXX )
SRCDIR="${SCRIPTDIR}/src"

readonly \
  SCRIPT_NAME \
  SCRIPTDIR \
  TMPDIR \
  SRCDIR

# alias to simplify checking if a command is in path
alias exists="> /dev/null type -p"


# sources
. ${SRCDIR}/options
. ${SRCDIR}/dependencies
. ${SRCDIR}/installd

# remove TMPDIR on exit ensuring that it matches spec first
finish () {
  {
    if [[ ${TMPDIR} =~ '^/tmp/doi\.([0-9a-zA-Z]){16}$' ]]; then
      rm -rf --one-file-system --preserve-root ${TMPDIR}
    fi
  } &> /dev/null
}

finish::failed () {
  finish
  exit 1
}

finish::success () {
  finish
  exit 0
}

dot_err () {
  print -u2 "error:" "$@"
}

usage () {
  cat <<EOF
Usage: ${SCRIPT_NAME} <command> <target> [options]

COMMANDS:
  ln   Create symlinks for selected dotfiles (default: zsh only)
  rm   Remove symlinks and any files created during ln

TARGETS:
  -a   Install all     dotfiles
  -z   Install Zsh     dotfiles
  -t   Install Tmux    dotfiles
  -g   Install GHCi    dotfiles
  -n   Install Neovim  dotfiles
  -x   Install X11     dotfiles

Options:
  -i   Install package dependencies for chosen dotfiles (ln only)
  -h   Print this usage message
EOF
}

main () {
  # exit trap for consistently clean exit
  trap "trap - SIGTERM && finish && kill -- -$$" \
    SIGINT SIGTERM

  local -r \
    ZSHDIR="${SCRIPTDIR}/zsh" \
    GHCIDIR="${SCRIPTDIR}/ghci" \
    NVIMDIR="${SCRIPTDIR}/neovim" \
    X11DIR="${SCRIPTDIR}/xmonad" \
    TMUXDIR="${SCRIPTDIR}/tmux" \
    DISTDIR="${SCRIPTDIR}/dist" \
    BINDIR="${HOME}/.local/bin"

# respect users current $XDG_CONFIG_HOME if set
if [[ -v ${XDG_CONFIG_HOME} ]]; then
  local -r CONFIGDIR="${XDG_CONFIG_HOME}"
else
  local -r CONFIGDIR="${HOME}/.config"
fi

  { [[ $ZSH_VERSION > 5.2 ]] || [[ $ZSH_VERSION == 5.2 ]] } \
    || {
      printf "zsh version must be 5.2 or higher\n"
      exit
    }

  local CMD

  local -Ua \
    missing_dependencies \

  local -ar \
    flags=( all zsh tmux ghci neovim x11 install_deps )

  local -Ua shortflags

  # create short flags aray with only first letter of every flag as element
  for sflag in ${flags}; do
    shortflags+=${sflag:0:1}
  done

  # distro dependencies for arch and ubuntu
  local -Ar YAOURT_DEPENDENCIES=( \
    zsh     "antibody fzf hub tig lsof trash-cli git" \
    ghci    "ghc haskell-stack haskell-hscolour haskell-pretty-show" \
    tmux    "tmux bind-tools acpi git powerline-fonts-git" \
    neovim  "neovim python-neovim python \
              ghc-mod rust-racer clang shellcheck \
              eslint tidy go git" \
    x11     "xmonad qutebrowser mpv xss-lock-git xautolock \
              xbanish unclutter-xfixes-git slock xorg-xsetroot \
              compton xmonad-contrib maim farbfeld xorg-xset git"
  )

  local -Ar APT_DEPENDENCIES=( \
    zsh     "antibody lsof trash-cli hub tig git" \
    ghci    "ghc haskell-stack libghc-hscolour-dev libghc-pretty-show-dev" \
    tmux    "tmux acpi dnsutils git" \
    neovim  "neovim python3 python3-pip ghc-mod clang shellcheck \
              tidy golang git" \
    x11     "xmonad mpv xss-lock xautolock suckless-tools \
              compton libghc-xmonad-contrib-dev maim \
              x11-xserver-utils qutebrowser git"
  )

  options $@

  dependencies

  installd

  finish
}

 main $@
